buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "nl.team_goliath.app"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            proto {
                // In addition to the default 'src/main/proto'
                srcDir '../third_party/proto'
            }
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def lifecycle_version = "2.0.0-alpha1"

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    def support_version = "1.0.0-alpha3"

    /* Android Support Libs */
    implementation "androidx.appcompat:appcompat:$support_version"
    implementation "com.google.android.material:material:$support_version"
    implementation "androidx.vectordrawable:vectordrawable:$support_version"
    implementation "androidx.preference:preference:$support_version"

    /* ViewPager */
    implementation "androidx.viewpager:viewpager:$support_version"

    /* Constraint layout */
    implementation 'androidx.constraintlayout:constraintlayout:1.1.1'

    /* Joystick */
    implementation 'io.github.controlwear:virtualjoystick:1.9.2'
    implementation 'me.angrybyte.slider:slider:1.4.2'

    /* Logging */
    implementation 'com.jakewharton.timber:timber:4.7.0'

    /* Protocol Buffers */
    implementation 'com.google.protobuf:protobuf-java:3.5.1'

    /* ZeroMQ Message Queue */
    implementation 'org.zeromq:jeromq:0.4.3'

    /* Testing */
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha3'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1-1'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
        }
    }
}
